plugins {
    id 'application'
    id 'com.google.protobuf' version '0.9.1'
    id 'idea'
}

repositories {
    maven { 
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.54.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.21.7'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // implementation 'net.sourceforge.tess4j:tess4j:5.7.0'
    // implementation 'net.sourceforge.lept4j:lept4j:1.18.0'
    // implementation 'com.google.zxing:core:3.5.1'
    // implementation 'com.google.zxing:javase:3.5.1'
    
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:3.4.0"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task idetectGrpcServer(type: CreateStartScripts) {
    mainClass = 'com.nurulhudaapon.idetect.IdetectServer'
    applicationName = 'idetect-grpc-server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

task idetectGrpcClient(type: CreateStartScripts) {
    mainClass = 'com.nurulhudaapon.idetect.IdetectClient'
    applicationName = 'idetect-grpc-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(idetectGrpcServer)
    from(idetectGrpcClient)
    fileMode = 0755
}
